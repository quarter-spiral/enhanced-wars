radio = require('radio')
$ = require('jquery')

@CAAT.DEBUG = false

class GameRenderer
  SCENE_OFFSET:
    x: 0
    y: 0

  constructor: (@game) ->
    self = @

    win = $(window)
    windowWidth = win.width()
    windowHeight = win.height()

    CANVAS_DIMENSIONS =
      width: windowWidth
      height: windowHeight

    CAAT.Module.Initialization.QSTemplate.init(
      CANVAS_DIMENSIONS.width, CANVAS_DIMENSIONS.height,

      # and will be added to the end of document. set an id of a canvas or div element
      'game',

      # load these images and set them up for non splash scenes.
      # image elements must be of the form:
      # {id:'<unique string id>',    url:'<url to image>'}
      # No images can be set too.
      [],

      # onEndSplash callback function.
      # Create your scenes on this method.
      (director) ->
        div = $('#game')
        # div.css('margin-left': "#{-0.5 * div.width()}px", 'margin-top': "#{-0.5 * div.height()}px")
        div.show()

        self.preloader = new CAAT.Module.Preloader.Preloader()

        self.director = director

        # create an offset adjusting container
        scene = director.createScene()
        container = new CAAT.Foundation.ActorContainer()
        scene.addChild(container)
        scene.setFillStyle('white')
        container.setSize(scene.width, scene.height)
        # nobody knows where that 32x32 offset is coming from but we need to correct it
        container.setLocation(self.SCENE_OFFSET.x, self.SCENE_OFFSET.y)
        # set the container as a fake scene
        self.scene = container

        self.mapContainer = new CAAT.Foundation.ActorContainer().
            setSize(CANVAS_DIMENSIONS.width, CANVAS_DIMENSIONS.height).
            setClip(true)
        container.addChild(self.mapContainer)
        self.mapContainer.centerAt(CANVAS_DIMENSIONS.width / 2, CANVAS_DIMENSIONS.height / 2)

        self.renderers = {}
        MapRenderer  = require('MapRenderer')
        self.renderers.map = new MapRenderer(self.game, self, self.mapContainer)
        UnitRenderer  = require('UnitRenderer')
        self.renderers.unit = new UnitRenderer(self.game, self, self.mapContainer)

        InputController  = require('InputController')
        self.inputController = new InputController(self, self.mapContainer)

        ScrollHandler = require('ScrollHandler')
        self.scrollHandler = new ScrollHandler(self.renderers.map)

        UIRenderer  = require('UIRenderer')
        self.renderers.ui = new UIRenderer(self.game, self)

        self.preloader.load (images) ->
          director.setImagesCache(images)
          radio("ew/renderer/assets-loaded").broadcast(self, images)
          self.ready = true

        inputEnabledRenderers = [
          self.renderers.unit
          self.renderers.map
        ]

        radio("ew/input/click").subscribe (e) ->
          for renderer in inputEnabledRenderers
            return unless renderer.click(e)

        radio("ew/input/doubleClick").subscribe (e) ->
          for renderer in inputEnabledRenderers
            return if renderer.doubleClick and !renderer.doubleClick(e)

        originalRatio = CANVAS_DIMENSIONS.height / (1.0 * CANVAS_DIMENSIONS.width)
        win.on('resize', ->
          winWidth = win.width() * 1.0
          winHeight = win.height() * 1.0

          newWidth = winWidth
          newHeight = winWidth * originalRatio
          if newHeight > winHeight
            newHeight = winHeight
            newWidth = newHeight / originalRatio

          xScale = newWidth / CANVAS_DIMENSIONS.width
          yScale = newHeight / CANVAS_DIMENSIONS.height
          director.setScale(xScale, yScale)
        )

        CAAT.loop()
    )

  update: =>
    @renderers.unit.updateUnits()

exports 'GameRenderer', GameRenderer